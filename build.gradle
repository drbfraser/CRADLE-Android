// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.30'
    ext.detekt_version = '1.14.1'
    // Important: Android Studio will not detect updates for Hilt; `hilt_version` should be checked
    // manually for updates alongside `hilt_jetpack_version`. Otherwise, we may run into issues
    // where hilt_jetpack_version expects a later version of Hilt but doesn't warn you about it and
    // just constantly fails to build with vague errors that can look like
    // "error:[Dagger/MissingBinding] ... cannot be provided without an @Provides-annotated method"
    // :(
    ext.hilt_version = '2.31.2-alpha'
    ext.hilt_jetpack_version = '1.0.0-alpha03'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        // TODO: Before this is ever used in production with real patient data, we need to evaluate
        //       Google's privacy policy for these things, since we're going to be dealing with
        //       personal and medical data. Likely better to just remove it entirely.
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
        classpath 'com.google.firebase:perf-plugin:1.3.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("$detekt_version")
}

allprojects {
    apply from: "$rootDir/detekt.gradle"

    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Log testing output
//   ref: https://stackoverflow.com/a/31665341
tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}
