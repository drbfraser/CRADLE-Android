// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.32'
    ext.detekt_version = '1.14.1'
    // Important: Android Studio will not detect updates for Hilt; `hilt_version` should be checked
    // manually for updates alongside `hilt_jetpack_version`. Otherwise, we may run into issues
    // where hilt_jetpack_version expects a later version of Hilt but doesn't warn you about it and
    // just constantly fails to build with vague errors that can look like
    // "error:[Dagger/MissingBinding] ... cannot be provided without an @Provides-annotated method"
    // :(
    ext.hilt_version = '2.33-beta'
    ext.hilt_jetpack_version = '1.0.0-alpha03'

    repositories {
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        // TODO: Before this is ever used in production with real patient data, we need to evaluate
        //       Google's privacy policy for these things, since we're going to be dealing with
        //       personal and medical data. Likely better to just remove it entirely.
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
        classpath 'com.google.firebase:perf-plugin:1.3.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("$detekt_version")
}

allprojects {
    apply from: "$rootDir/detekt.gradle"

    repositories {
        google()
        mavenCentral()
        jcenter {
            // JCenter is being deprecated.
            // https://jfrog.com/blog/into-the-sunset-bintray-jcenter-gocenter-and-chartcenter/
            // Restrict us from using it for any new dependencies; only allow the usage of JCenter
            // for these dependencies that have not been migrated off of it.
            // TODO: Remove JCenter all together when all packages here have migrated off of it.
            content {
                includeModule("org.tensorflow", "tensorflow-lite")
                includeModule("org.tensorflow", "tensorflow-lite-support")
                // Required for detekt
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
                // TODO: Remove trouve4j from here when Android Gradle Plugin is updated
                //  https://youtrack.jetbrains.com/issue/IDEA-261387
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Log testing output
//   ref: https://stackoverflow.com/a/31665341
tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}
